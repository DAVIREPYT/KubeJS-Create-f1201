plugins {
	id "dev.architectury.loom" version "1.6-SNAPSHOT"
	id "maven-publish"
	id "me.shedaniel.unified-publishing" version "0.1.+"
}

apply from: 'https://files.latmod.com/public/markdown-git-changelog.gradle'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '17'

def ENV = System.getenv()
version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: '9999'}"
archivesBaseName = "${project.archives_base_name}-fabric"
group = project.maven_group

loom {
	silentMojangMappingsLicense()
}

repositories {
	maven {
		url "https://maven.saps.dev/releases"
	}

	// the maven for the devOS repo, for create fabric
	// (and a bunch of libraries I'm assuming)
	maven { url "https://mvn.devos.one/snapshots/" }
	maven { url "https://mvn.devos.one/releases/" }

	maven { url = "https://maven.terraformersmc.com/releases/" } // Mod Menu
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes

	maven {
		url "https://maven.tterrag.com/"
		content {
			includeGroup "com.jozufozu.flywheel"
		}
	}

	maven {
		name = "Fuzs Mod Resources"
		url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
	}

	// jitpack?! why?!
	maven {
		url "https://jitpack.io/"
		content {
			includeGroup "com.github.LlamaLad7"
			includeGroup "com.github.llamalad7.mixinextras" // two different versions of mixinextras on the classpath?? WHY?!
			includeGroup "com.github.Chocohead"
			includeGroup "com.github.Draylar.omega-config"
		}
	}

	// "i play both sides, that way i always come out ahead"
	maven {
		url "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
	mappings loom.officialMojangMappings()

	modApi("net.fabricmc:fabric-loader:${rootProject.loader_version}")
	modApi("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}")

	modApi "dev.latvian.mods:kubejs-fabric:${rootProject.kubejs_version}"
	modApi "com.simibubi.create:create-fabric-${rootProject.minecraft_version}:${rootProject.create_version}"
	modApi("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:8.0.0")

	modLocalRuntime modCompileOnly("curse.maven:jei-238222:5440260")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
}

jar {
	archiveClassifier.set "dev"
}

remapJar {
    archiveBaseName.set "${rootProject.archives_base_name}-fabric"
    archiveClassifier.set null
}

publishing {
	publications {
		mavenFabric(MavenPublication) {
			artifactId = rootProject.archivesBaseName
			from components.java
		}
	}
}



unifiedPublishing {
	project {
		releaseType = "${ENV.RELEASE_TYPE ?: 'release'}"
		gameVersions = [
				rootProject.minecraft_version
		]
		gameLoaders = ["fabric"]
		displayName = "$mod_name Fabric $project.version"
		changelog = getGitChangelog()
		mainPublication remapJar

		relations {
			depends {
				curseforge = modrinth = "kubejs"
            }
            depends {
                curseforge = modrinth = "create-fabric"
            }
		}
	}
}
